import org.gradle.api.artifacts.maven.MavenDeployment

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.0'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    group 'com.github.eoinf.jiggen'

    ext {
        appName = "Jiggen"
        gdxVersion = '1.9.8'
        roboVMVersion = '2.3.1'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.1'

        rxJavaVersion = '2.1.17'

        junitPlatformVersion = '1.0.2'
        junitJupiterVersion  = '5.0.2'
        log4jVersion         = '2.9.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url = 'https://raw.githubusercontent.com/intendia-oss/rxjava-gwt/mvn-repo/' }
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":core") {
    apply plugin: "java"
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceSets.main.java.srcDirs = [ "src/" ]

    dependencies {
        compile gradleApi()
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

        // JUnit Jupiter API and TestEngine implementation
        testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")

        // To avoid compiler warnings about @API annotations in JUnit code
        testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')

        // To use Log4J's LogManager
        testRuntime("org.apache.logging.log4j:log4j-core:${log4jVersion}")
        testRuntime("org.apache.logging.log4j:log4j-jul:${log4jVersion}")

        // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
        testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
    }

    junitPlatform {
        // platformVersion '1.0.2'
        filters {
            engines {
                // include 'junit-jupiter', 'junit-vintage'
                // exclude 'custom-engine'
            }
            tags {
                // include 'fast'
                exclude 'slow'
            }
            // includeClassNamePattern '.*Test'
        }
        // enableStandardTestTask true
        // reportsDir file('build/test-results/junit-platform') // this is the default
        logManager 'org.apache.logging.log4j.jul.LogManager'
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compile
    }

    task ('sourcesJar', type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task ('javadocJar', type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from tasks.javadoc.destinationDir
    }

    task('dist', type: Jar, dependsOn: 'classes') {
        from files(sourceSets.main.output.classesDir)
        from files(sourceSets.main.output.resourcesDir)

        // Skip the creation of the default jar artifact
        jar.enabled = false
    }

    signing {
        sign project.configurations.archives
    }

    task('archiveAll') {
        dependsOn 'dist'
        dependsOn 'sourcesJar'
        dependsOn 'javadocJar'
        doLast {
            project.artifacts {
                archives dist
                archives sourcesJar
                archives javadocJar
            }
        }
    }

    task('publishStaging', type: Upload, dependsOn: 'archiveAll') {
        repositories {
            configuration = project.configurations.archives
            mavenDeployer {
                //def ascPom = addFilter('asc') {artifact, file -> artifact.ext == "asc" }
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Jiggen'
                    packaging 'jar'
                    description 'Core code for jiggen'
                    url 'https://github.com/EoinF/jiggen'

                    scm {
                        url 'scm:git@github.com:EoinF/jiggen.git'
                        connection 'scm:git@github.com:EoinF/jiggen.git'
                        developerConnection 'scm:git@github.com:EoinF/jiggen.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'outterest'
                            name 'Eoin Flanagan'
                        }
                    }
                }
            }
        }
    }
}

tasks.eclipse.doLast {
    delete ".project"
}